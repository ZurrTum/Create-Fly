plugins {
    id "net.fabricmc.fabric-loom" version "${loom_version}"
}

repositories {
    maven {
        name "REI"
        url "https://maven.shedaniel.me/"
        content {
            includeGroup "me.shedaniel"
            includeGroup "me.shedaniel.cloth"
            includeGroup "dev.architectury"
        }
    }
    maven {
        url "https://oss.zurrtum.com"
        content {
            includeGroup("cc.tweaked")
        }
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroup "dev.onyxstudios.cardinal-components-api"
            includeGroup "org.ladysnake.cardinal-components-api"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

version = "$project.mod_version-$project.mod_id"
group = project.maven_group

base {
    archivesName = "$project.archives_base_name-$project.minecraft_version"
}

loom {
    splitEnvironmentSourceSets()
    accessWidenerPath = file("src/main/resources/create.accesswidener")
    mods {
        create {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

sourceSets {
    main {
        java {
            exclude 'com/zurrtum/create/compat/computercraft/implementation/**'
            exclude 'com/zurrtum/create/compat/computercraft/AllComputerDisplaySource.java'
            exclude 'com/zurrtum/create/compat/computercraft/AllComputerPeripherals.java'
            exclude 'com/zurrtum/create/compat/CompatMod.java'
            exclude 'com/zurrtum/create/compat/rei/**'
            exclude 'com/zurrtum/create/compat/eiv/**'
            exclude 'com/zurrtum/create/compat/trinkets/**'
            exclude 'com/zurrtum/create/mixin/CreateIntegrationMixin.java'
            exclude 'com/zurrtum/create/mixin/ArchitecturyMixin.java'
            exclude 'com/zurrtum/create/mixin/JustEnoughItemsMixin.java'
        }
    }
    client {
        java {
            exclude 'com/zurrtum/create/client/compat/jei/**'
            exclude 'com/zurrtum/create/client/compat/rei/**'
            exclude 'com/zurrtum/create/client/compat/eiv/**'
            exclude 'com/zurrtum/create/client/compat/sodium/**'
            exclude 'com/zurrtum/create/client/compat/trinkets/**'
            exclude 'com/zurrtum/create/client/compat/CompatMod.java'
            exclude 'com/zurrtum/create/client/mixin/FabricModelAccessMixin.java'
            exclude 'com/zurrtum/create/client/mixin/IrisPipelinesMixin.java'
            exclude 'com/zurrtum/create/client/mixin/HoldMyItemsMixin.java'
            exclude 'com/zurrtum/create/client/mixin/AnimationResourceLoaderMixin.java'
            exclude 'com/zurrtum/create/client/mixin/CraftingViewRecipeAccessor.java'
            exclude 'com/zurrtum/create/client/mixin/FabricEIVMixin.java'
            exclude 'com/zurrtum/create/client/mixin/FluidItemSpecialRendererMixin.java'
            exclude 'com/zurrtum/create/client/mixin/ItemSlotMixin.java'
            exclude 'com/zurrtum/create/client/mixin/RecipeViewMenuMixin.java'
            exclude 'com/zurrtum/create/client/mixin/RecipeViewScreenMixin.java'
            exclude 'com/zurrtum/create/client/mixin/ViewTypeButtonMixin.java'
            exclude 'com/zurrtum/create/client/mixin/AbstractBlockRenderContextMixin.java'
        }
    }
}

tasks.register("serverJar", Jar) {
    archiveClassifier = "server"
    from sourceSets.main.output
    from("LICENSE")
    from("licenses") {
        into "licenses"
    }
}

tasks.build {
    dependsOn tasks.serverJar
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    implementation "net.fabricmc:fabric-loader:${project.loader_version}"
    if (project.compile_only_fabric_api.equals("true")) {
        compileOnly fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)
        compileOnly fabricApi.module("fabric-transfer-api-v1", project.fabric_version)
        compileOnly fabricApi.module("fabric-events-interaction-v0", project.fabric_version)
        compileOnly fabricApi.module("fabric-renderer-api-v1", project.fabric_version)
        compileOnly fabricApi.module("fabric-renderer-indigo", project.fabric_version)
        compileOnly fabricApi.module("fabric-recipe-api-v1", project.fabric_version)
        compileOnly fabricApi.module("fabric-model-loading-api-v1", project.fabric_version)
    } else {
        implementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    }
//    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
//    if (project.compile_only_rei.equals("false")) {
//        modClientRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
//    }
//    modCompileOnly "maven.modrinth:jei:${project.jei_version}"
//    modCompileOnly "maven.modrinth:eiv:${project.eiv_version}"
//    modCompileOnly "cc.tweaked:cc-tweaked-${project.cct_version}"
//    modCompileOnly "maven.modrinth:trinkets-canary:${project.trinkets_version}"
//    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
//    modClientCompileOnly "maven.modrinth:sodium:${project.sodium_version}"
//    modClientCompileOnly "maven.modrinth:iris:${project.iris_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }

    exclude "_data"
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 25
}

java {
    withSourcesJar()
    tasks.named("sourcesJar", Jar) {
        from("LICENSE")
        from("licenses") {
            into "licenses"
        }
        exclude '_data'
        exclude 'assets'
        exclude 'data'
    }
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE")
    from("licenses") {
        into "licenses"
    }
}